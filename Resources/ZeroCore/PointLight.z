// Data needed for rendering a point light.
// Usage with DeferredRenderer expects a Model on this object with the
// 'PointLight' Material and 'Sphere' Mesh.
// No default usage with ForwardRenderer.
[RunInEditor]
class PointLight : ZilchComponent
{
  [Dependency] var Transform : Transform;
  
  // Color used in lighting calculations.
  [Property] var LightColor : Real4
  {
    get { return Real4(this.LightColorValue, 1.0); }
    set { this.LightColorValue = value.XYZ; }
  }
  
  // Combined with area light falloff and represents total emittance of the light.
  [Property][ShaderInput("PointLightFragment")] var LightIntensity : Real
  {
    get { return this.LightIntensityValue; }
    set { this.LightIntensityValue = Math.Max(value, 0.0); }
  }
  
  // Distance at which the light's effect is completely faded.
  // Does not affect apparent brightness of the light.
  [Property][ShaderInput("PointLightFragment")] var FalloffRadius : Real
  {
    get { return this.FalloffRadiusValue; }
    set
    {
      this.FalloffRadiusValue = Math.Max(value, 0.1);
      this.SetLightRadiusInput();
      // Don't have access to other components in serialization.
      if (this.Transform != null)
        this.Transform.WorldScale = Real3(this.FalloffRadiusValue * 2.0);
    }
  }
  
  // Physical radius of the sphere of light.
  // Affects the shape of specular reflection.
  [Property] var LightRadius : Real
  {
    get { return this.LightRadiusValue; }
    set
    {
      this.LightRadiusValue = Math.Max(value, 0.0);
      this.SetLightRadiusInput();
    }
  }
  
  [ShaderInput("PointLightFragment", "LightRadius")] var LightRadiusInput : Real;
  [ShaderInput("LightPass", "LightColor")] var LightColorValue : Real3 = Real3(1.0);
  var LightIntensityValue : Real = 20.0;
  var FalloffRadiusValue : Real = 10.0;
  var LightRadiusValue : Real = 0.0;
  
  function Initialize(init : CogInitializer)
  {
    this.Transform.WorldScale = Real3(this.FalloffRadiusValue * 2.0);
    this.SetLightRadiusInput();
  }
  
  function SetLightRadiusInput()
  {
    this.LightRadiusInput = Math.Min(this.LightRadiusValue, this.FalloffRadius);
  }
  
  function DebugDraw()
  {
    var position = this.Transform.WorldTranslation;
    var lightSphere = DebugSphere(position, this.LightRadiusInput) {Color = Real4(0.5, 0.5, 0.0, 1.0)};
    var falloffSphere = DebugSphere(position, this.FalloffRadius) {Color = Real4(1.0, 1.0, 0.0, 1.0)};
    DebugDraw.Add(lightSphere);
    DebugDraw.Add(falloffSphere);
  }
}
